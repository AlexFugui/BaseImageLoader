apply plugin: 'com.github.dcendents.android-maven'
apply plugin: 'com.jfrog.bintray'

def siteUrl = 'https://github.com/AlexFugui/BaseImageLoader' //项目在github主页地址
def gitUrl = 'https://github.com/AlexFugui/BaseImageLoader.git' //Git仓库的地址
group = "com.alex"
version = "1.1.0"


install {
    repositories.mavenInstaller {
        pom {
            project {
                packaging 'aar'//打包类型 aar文件
                name 'BaseImageLoader'// 在pom库中的包名
                description 'Android BaseImageLoader'// 可选，项目描述。
                url siteUrl // 项目主页

                licenses {
                    license {
                        name 'The Apache Software License, Version 2.0'
                        url 'http://www.apache.org/licenses/LICENSE-2.0.txt'
                    }
                }

                developers {
                    developer {
                        id 'AlexFugui' // 开发者的id。
                        name 'AlexFugui' // 开发者名字。
                        email '329406095@qq.com' // 开发者邮箱。
                    }
                }

                scm {
                    connection gitUrl // Git仓库地址。
                    developerConnection gitUrl // Git仓库地址。
                    url siteUrl // 项目主页。
                }
            }
        }
    }
}

task sourcesJar(type: Jar) {
    from android.sourceSets.main.java.srcDirs
    getArchiveClassifier().convention("sources")
    getArchiveClassifier().set("sources")
}

task javadoc(type: Javadoc) {
    source = android.sourceSets.main.java.srcDirs
    classpath += project.files(android.getBootClasspath().join(File.pathSeparator))
    failOnError false
}

task javadocJar(type: Jar, dependsOn: javadoc) {
    getArchiveClassifier().convention("javadoc")
    getArchiveClassifier().set("javadoc")
    from javadoc.destinationDir
}
artifacts {
    archives javadocJar
    archives sourcesJar
}

Properties properties = new Properties()
properties.load(project.rootProject.file('local.properties').newDataInputStream())
bintray {
    user = properties.getProperty("bintray.user")
    key = properties.getProperty("bintray.apikey")

    configurations = ['archives']
    pkg {
        repo = "maven"
        name = "BaseImageLoader"//项目上传以后在名为maven的库中的包名
        userOrg = 'alexfugui'//Bintray的组织中 id
        websiteUrl = siteUrl
        vcsUrl = gitUrl
        licenses = ["Apache-2.0"]
        publish = true // 是否是公开项目。
    }
}
